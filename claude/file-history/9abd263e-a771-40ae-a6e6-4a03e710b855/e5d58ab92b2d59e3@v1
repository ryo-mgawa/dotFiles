module "load_balancer" {
  source = "../../modules/load_balancer"

  loogia_execution_domain = "operation.loogia.tech"
  web_front_bucket_name   = module.storage_bucket.web_front_bucket_complete_name
  actual_bucket_name      = module.image.actual_image_bucket_name
  contents_security_policy = [
    "Content-Security-Policy: ${local.content_security_policy};",
    "Report-To: {{\"group\":\"csp-endpoint\",\"max_age\":10886400,\"endpoints\":[{{\"url\":\"https://o232019.ingest.us.sentry.io/api/4505226598547456/security/?sentry_key=376b866c3c0b4c52a11a294737a7a67a&sentry_environment=production\"}}],\"include_subdomains\":true}}",
    "Reporting-Endpoints: csp-endpoint=\"https://o232019.ingest.us.sentry.io/api/4505226598547456/security/?sentry_key=376b866c3c0b4c52a11a294737a7a67a&sentry_environment=production\"",
    "X-Content-Type-Options: nosniff",
    "X-Frame-Options: DENY"
  ]
}

module "github" {
  source          = "../../modules/github"
  github_sa_email = module.service_account.github_ci_service_account_email
}

module "operation" {
  source = "../../modules/operation"
}

module "google_api" {
  source = "../../modules/google_api"

  service_api_list = local.service_api_list
}

module "api" {
  source = "../../modules/api"

  cloud_run_sa                    = module.service_account.run_service_account_email
  alloydb_cluster_id              = module.alloy_db.alloydb_cluster_id
  alloydb_instance_id             = module.alloy_db.alloydb_instance_id
  api_secret_env_id               = module.secret_manager.api_secret_env_id
  readonly_api_secret_env_id      = module.secret_manager.readonly_api_secret_env_id
  cloud_run_images_sa             = module.service_account.run_images_service_account_email
  alloydb_network_name            = module.alloy_db.alloydb_network_name
  alloydb_network_id              = module.alloy_db.alloydb_network_id
  api_subnet_cidr                 = local.subnets["api"]
  readonly_api_subnet_cidr        = local.subnets["readonly_api"]
  api_min_instance_count          = 1
  readonly_api_min_instance_count = 1
}

module "housekeep" {
  source = "../../modules/housekeep"

  cloud_run_sa                = module.service_account.run_service_account_email
  cloud_run_housekeep_sa      = module.service_account.run_housekeep_service_account_email
  scheduler_sa                = module.service_account.eventarc_receiver_service_account_email
  cloud_run_housekeep_sa_name = module.service_account.run_housekeep_service_account_name
  housekeep_secret_env_id     = module.secret_manager.housekeep_secret_env_id
  ap_elt_runners = [
    "elt-runner@logistics-insights-prd.iam.gserviceaccount.com",
    "elt-runner@logistics-insights-stg.iam.gserviceaccount.com"
  ]
  alloydb_network_name  = module.alloy_db.alloydb_network_name
  alloydb_network_id    = module.alloy_db.alloydb_network_id
  housekeep_subnet_cidr = local.subnets["housekeep"]
  min_instance_count    = 1
}

module "service_account" {
  source = "../../modules/service_account"
}

module "storage_bucket" {
  source = "../../modules/cloud_storage"

  web_front_bucket_base_name = "loogia-execution-web-front-production"
}

module "image" {
  source = "../../modules/image"

  loogia_execution_domain     = "operation.loogia.tech"
  cloud_run_sa                = module.service_account.run_service_account_email
  cloud_run_sa_name           = module.service_account.run_service_account_name
  cloud_run_images_sa         = module.service_account.run_images_service_account_email
  images_secret_env_id        = module.secret_manager.images_secret_env_id
  temporary_image_bucket_name = "temporary-image-storage"
  actual_image_bucket_name    = "actual-image-storage"
  slack_alert_channel_id      = "16003855528276190567"
  alloydb_network_name        = module.alloy_db.alloydb_network_name
  alloydb_network_id          = module.alloy_db.alloydb_network_id
  image_subnet_cidr           = local.subnets["image"]
  min_instance_count          = 1
}

module "spot_master" {
  source = "../../modules/spot_master"

  alloydb_cluster_id        = module.alloy_db.alloydb_cluster_id
  alloydb_instance_id       = module.alloy_db.alloydb_instance_id
  spot_master_secret_env_id = module.secret_manager.spot_master_secret_env_id
  cloud_run_sa              = module.service_account.run_service_account_email
  cloud_run_spot_master_sa  = module.service_account.run_spot_master_service_account_email
  slack_alert_channel_id    = "16003855528276190567"
  alloydb_network_name      = module.alloy_db.alloydb_network_name
  alloydb_network_id        = module.alloy_db.alloydb_network_id
  spot_master_subnet_cidr   = local.subnets["spot_master"]
  min_instance_count        = 1
}

module "notification" {
  source = "../../modules/notification"

  project_services_ready = module.google_api.project_services_ready
}

module "alloy_db" {
  source = "../../modules/alloy_db"

  vpc_subnet_range                      = "10.254.0.0/16"
  machine_type                          = "e2-micro"
  gce_ip                                = "10.254.0.3"
  gce_tag                               = "workspace"
  vpc_serverless_connector_machine_type = "e2-micro"
  readonly_db_cpu_count                 = 2
  api_user_id                           = "api-user"
  readonly_api_user_id                  = "readonly-api-user"
  housekeep_user_id                     = "housekeep-user"
  images_user_id                        = "images-user"
  is_ha_enabled                         = true
}

module "artifact_registry" {
  source = "../../modules/artifact_registry"
}

module "secret_manager" {
  source = "../../modules/secret_manager"
}

module "pubsub" {
  source                = "../../modules/pubsub"
  push_datadog_endpoint = "https://gcp-intake.logs.datadoghq.com/api/v2/logs?dd-api-key=${var.datadog_api_key}&dd-protocol=gcp"
}

module "logging" {
  source = "../../modules/logging"

  pubsub_export_logs_topic_name       = module.pubsub.pubsub_export_logs_topic_name
  default_log_bucket_retention_days   = 365
  default_log_bucket_enable_analytics = false
}

module "firebase" {
  source = "../../modules/firebase"

  project_services_ready = module.google_api.project_services_ready
}

module "feature_flag" {
  source = "../../modules/feature_flag"
}

module "application" {
  source = "../../modules/application"

  bundle_id              = "tech.optimind.loogia.driver-operation"
  package_name           = "tech.optimind.loogia.driver_operation"
  sha1_hashes            = var.sha1_hashes
  sha256_hashes          = var.sha256_hashes
  android_key_name       = "0099f31b-746c-4d98-b3ce-c7c989320528"
  ios_key_name           = "1ed4c80b-d317-4cad-92dc-545b8c62993c"
  web_operation_key_name = "822bcd63-78dd-4218-9881-b6020b7f346f"
  web_allowed_referrers = [
    "operation.loogia.tech/*"
  ]
}
